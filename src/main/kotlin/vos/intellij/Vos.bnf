{
    parserClass = "vos.intellij.language.parser.JssParser"
    parserUtilClass = "vos.intellij.language.psi.JssParserUtil"

    extends = "com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix = "Jss"
    psiImplClassSuffix = "Node"
    psiPackage = "vos.intellij.language.psi"
    psiImplPackage = "vos.intellij.language.psi_node"

    elementTypeHolderClass = "vos.intellij.language.psi.VosTypes"
    elementTypeClass = "vos.intellij.language.psi.JssElementType"
    tokenTypeClass = "vos.intellij.language.psi.JssElementType"

    psiImplUtilClass = "vos.intellij.language.mixin.NodeExtension"
    generateTokenAccessors = false

    tokens = [
        // comment
        COMMENT_DOCUMENT = 'DocumentComment'
        COMMENT = 'DocumentInline'
        COMMENT_BLOCK = 'DocumentBlock'
        // literal
        URL = 'Url'
        SYMBOL = 'Symbol'
        REFERENCE = 'Reference'
        STRING = 'String'
        BYTE = 'Byte'
        INTEGER = 'Integer'
        DECIMAL = 'Decimal'
        SIGN = 'regexp:[+-]'

        PARENTHESIS_L = '('
        PARENTHESIS_R = ')'
        BRACKET_L = '['
        BRACKET_R = ']'
        BRACE_L = '{'
        BRACE_R = '}'
        ANGLE_L = '<'
        ANGLE_R = '>'
        ACCENT = '^'
        EQ = '='
        COLON = ':'
        SEMICOLON = ';'
        COMMA = ','
        DOLLAR = '$'
        DOT = '.'
        STAR = '*'
        AT = '@'
    ]
}

VOS ::= statement*;

private statement ::=
    schema_statement
  | property_statement
  | def_statement
  | annotation
//  | COMMENT_DOCUMENT
  | object
  | ignore;
private ignore ::= SEMICOLON | COMMA;
// ---------------------------------------------------------------------------------------------------------------------
// schema <ID>: <TYPE> { }
schema_statement ::= schema identifier [type_hint] schema_block {
    mixin = "vos.intellij.language.mixin.MixinSchema"
};
private schema_block ::= <<brace_block properties_inner ignore>>
schema ::= "schema"
// ---------------------------------------------------------------------------------------------------------------------
annotation_statement ::= annotation identifier [type_hint] annotation_block {
    mixin = "vos.intellij.language.mixin.MixinAnnotation"
};
private annotation_block ::= "annotation_block";
annotation ::= ANNOTATION_SYMBOL
// ---------------------------------------------------------------------------------------------------------------------
// properties <ID>: <TYPE> { }
property_statement ::= property key [type_hint] [properties_block] {
    mixin = "vos.intellij.language.mixin.MixinProperty"
};
property ::= "properties" | "property" | "prop" | "field" | DOT;
private properties_block ::= <<brace_block properties_inner ignore>>;
private properties_inner ::=
    property_statement
  | attribute_statement
  ;
// ---------------------------------------------------------------------------------------------------------------------
def_statement ::= ("def"|"define"|"definition") key [type_hint] [properties_block];
// ---------------------------------------------------------------------------------------------------------------------
// <KEY>: <VALUE>
attribute_statement ::= key set value {
    mixin = "vos.intellij.language.mixin.MixinAttribute"
};
// ---------------------------------------------------------------------------------------------------------------------
kv_pair ::= key set value;
key ::= RAW_STRING_1|RAW_STRING_2 | SYMBOL {
    mixin = "vos.intellij.language.mixin.MixinKey"
};
// ---------------------------------------------------------------------------------------------------------------------
value ::= null | boolean | num | string | array | object | url_maybe_valid {
    mixin = "vos.intellij.language.mixin.MixinValue"
};
null ::= "null";
boolean ::= "true" | "false";
// used in annotation
url_maybe_valid ::= URL;
// ---------------------------------------------------------------------------------------------------------------------
type_hint ::= COLON type_symbol;
private type_symbol ::= SYMBOL | STRING;
// ---------------------------------------------------------------------------------------------------------------------
set ::= EQ | COLON;
// ---------------------------------------------------------------------------------------------------------------------
private num ::= [SIGN] (INTEGER | DECIMAL) | [SIGN] BYTE;
// ---------------------------------------------------------------------------------------------------------------------
string ::= (RAW_STRING_1|RAW_STRING_2|RAW_STRING_3) {
    mixin = "vos.intellij.language.mixin.MixinString"
}
// ---------------------------------------------------------------------------------------------------------------------
array ::= <<bracket_block value ignore>> {
    mixin = "vos.intellij.language.mixin.MixinArray"
};
object ::= <<brace_block kv_pair ignore>>;
// ---------------------------------------------------------------------------------------------------------------------
namespace ::= identifier (DOT identifier)*;
identifier ::= SYMBOL;
// ---------------------------------------------------------------------------------------------------------------------
// parenthesis ( )
private meta parenthesis ::= PARENTHESIS_L <<param>> PARENTHESIS_R;
// bracket [ ]
meta bracket_block ::= BRACKET_L [<<item>> (<<sp>> <<item>>)* [<<sp>>]] BRACKET_R;
// brace { }
meta brace_block ::=BRACE_L (<<item>>|<<sp>>)* BRACE_R;

